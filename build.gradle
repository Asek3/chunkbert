buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        maven { url = 'https://maven.minecraftforge.net' }
    }
}

plugins {
    id "com.gtnewhorizons.retrofuturagradle" version "1.3.26"
}

group = 'com.embeddedt'
version = '0.1.0'
archivesBaseName = 'chunkbert'

minecraft {
    mcVersion.set(project.minecraft_version)
    username.set("Developer")
}

repositories {
    mavenCentral()
    maven {
        name 'SpongePowered Maven'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        url "https://maven.cleanroommc.com"
    }
}

dependencies {
    def mixinBooter = modUtils.enableMixins("zone.rong:mixinbooter:8.9", "mixins.chunkbert.refmap.json")
    api (mixinBooter) {
        transitive = false
    }
    annotationProcessor "org.ow2.asm:asm-debug-all:5.2"
    annotationProcessor "com.google.guava:guava:32.1.2-jre"
    annotationProcessor "com.google.code.gson:gson:2.8.9"
    annotationProcessor (mixinBooter) {
        transitive = false
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

def coreModManifest = {
    attributes "FMLCorePlugin": "com.embeddedt.chunkbert.core.ChunkbertPlugin"
    attributes "FMLCorePluginContainsFMLMod": true
    attributes "ForceLoadAsMod": true
    attributes "Maven-Artifact": "${group}:${name}:${version}"
}

jar {
    manifest coreModManifest

    from sourceSets.main.output

    rename '(.+_at.cfg)', 'META-INF/$1'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}